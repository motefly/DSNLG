@import numbers.nlg
@import locations.nlg
@import filler.nlg

%
    ~prefix? %sentence ~suffix?

%sentence=
    ~please? %command
    %sequence
    %if

%command
    %getState
    %setLightState
    %setSwitchState

%sequence
    %command and then %command

%if
    if %condition then %command

%condition
    %currentWeather is $operator $number 

%currentWeather
    the temperature in %location 

%getState=
    %getDeviceState
    %getWeather
    %getTime

%getDeviceState
    get the state of the $device_name
    what is the the state of the $device_name
    tell me about the $device_name

%getWeather
    what is the weather in %location
    how is it in %location
    tell me the weather of %location

%getTime
    what time is it in %location
    tell me what time it is in %location

%setLightState
    ~set the $light_name $light_state
    turn $on_off the $light_name
%setSwitchState
    ~set the $switch_name $on_off
    turn $on_off the $switch_name

~set
    set
    make
    turn

$device_name=
    $light_name
    $switch_name

$light_state=
    $on_off
    $light_color

$light_name
    office light
    living room light

$switch_name
    tea switch
    lamp switch

$light_color
    blue
    red
    green
    yellow
    purple
    orange
    white

$on_off
    on
    off
